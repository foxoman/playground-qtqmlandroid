isEmpty(QMLANDROID_PREFIX): \
    QMLANDROID_PREFIX = $$[QT_INSTALL_PREFIX]

pkg_paths = $$files($$QMLANDROID_PREFIX/src/android/java/native/qt/android/*)
for(pp, pkg_paths): \
    pkg_names += $$basename(pp)

for(pkg, QMLANDROID_PACKAGES) {
    pkg_path = $$QMLANDROID_PREFIX/src/android/java/native/qt/android/$$pkg
    !exists($$pkg_path): \
        error("Unknown QML for Android package '$$pkg'. Available packages: $$pkg_names")
    QMLANDROID_INPUT += $$pkg_path
}

defineReplace(qmlAndroidPkgOutputName) {
    name = $$basename(1)
    name = $$absolute_path($$name, $$OUT_PWD/android-build/src/qt/android/)
    name = $$relative_path($$name, $$_PRO_FILE_PWD_)
    return($${name})
}

qmlandroid.input = QMLANDROID_INPUT
qmlandroid.output = ${QMAKE_FUNC_FILE_IN_qmlAndroidPkgOutputName}
!contains(TEMPLATE, vc.*): qmlandroid.variable_out = PRE_TARGETDEPS
qmlandroid.commands = ( $$sprintf($$QMAKE_MKDIR_CMD, ${QMAKE_FILE_OUT}) ) && $$QMAKE_COPY_DIR ${QMAKE_FILE_IN} ${QMAKE_FILE_OUT}
qmlandroid.name = COPY ${QMAKE_FILE_IN}
qmlandroid.CONFIG = no_link no_clean
QMAKE_EXTRA_COMPILERS += qmlandroid
